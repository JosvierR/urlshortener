plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'edu.pucmm'
version = '1.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    // Dependencias de tu aplicaciÃ³n (Javalin, MongoDB, JWT, etc.)
    implementation 'io.javalin:javalin:5.6.1'
    implementation 'org.mongodb:mongodb-driver-sync:4.9.1'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.slf4j:slf4j-simple:2.0.7'

    // Dependencias para gRPC y Protobuf
    implementation 'io.grpc:grpc-netty-shaded:1.60.0'
    implementation 'io.grpc:grpc-protobuf:1.60.0'
    implementation 'io.grpc:grpc-stub:1.60.0'
    implementation 'com.google.protobuf:protobuf-java:3.25.0'

    // Dependencias para test
    testImplementation 'junit:junit:4.13.2'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.12"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.53.0"
        }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

application {
    // Indica la clase principal de tu servidor REST (con Javalin).
    mainClass = 'edu.pucmm.Main'
}

shadowJar{
    archiveBaseName.set("app")
    archiveClassifier.set('')
    archiveVersion.set('')
}
/**
 * tarea necesaria para heroku si optamos por la tarea de Shadowjar
 * en el archivo Procfile debe estar el siguiente comando:
 * web: java -jar build/libs/app.jar
 */
task stage {
    dependsOn shadowJar
}

test {
    useJUnitPlatform()
}
